// Copyright 2019-2020 LinkedIn Corporation. All rights reserved.
// Licensed under the BSD-2 Clause license.
// See LICENSE in the project root for license information.

plugins {
  id "com.diffplug.spotless" version "5.8.2"
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id "org.shipkit.java" version "2.3.4"
}

allprojects {
  group = 'com.linkedin.transport'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  spotless {
    enforceCheck true
    groovyGradle {
      target '**/*.gradle'
      greclipse().configFile("${rootDir}/../docs/codestyle-eclipse.xml")
    }
    format 'markdown', {
      target '**/*.md'
      prettier()
    }
  }
}

subprojects {
  project.ext.setProperty('presto-version', '333')
  project.ext.setProperty('airlift-slice-version', '0.38')
  project.ext.setProperty('spark-group', 'org.apache.spark')
  project.ext.setProperty('spark-version', '2.3.0')

  buildscript {
    repositories {
      mavenCentral()
      jcenter()
    }
  }

  repositories {
    mavenCentral()
    maven {
      url "https://conjars.org/repo"
    }
  }

  plugins.withType(JavaPlugin) {
    dependencies {
      testCompile 'org.testng:testng:6.11'
    }
    test {
      useTestNG()
    }
    spotless {
      java {
        eclipse().configFile("${rootDir}/../docs/codestyle-eclipse.xml")
        licenseHeaderFile file("${rootDir}/../gradle/license/LICENSE_HEADER")
      }
    }
  }

  plugins.withType(ScalaPlugin) {
    spotless {
      scala {
        scalafmt()
        licenseHeaderFile file("${rootDir}/../gradle/license/LICENSE_HEADER"), 'package'
      }
    }
  }
}
